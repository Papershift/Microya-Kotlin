apply plugin: 'jacoco'

jacoco {
    toolVersion = "$version_jacoco"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport) {
    group 'Reporting'
    description "Generate Jacoco coverage reports."

    reports {
        xml.enabled = true
        xml.destination file("${rootProject.buildDir}/codecov-report/jacocoTestReport.xml")

        html.enabled = true
        html.destination file("${rootProject.buildDir}/coverage-report")
    }

    def javaClasses = []
    def kotlinClasses = []
    def javaSrc = []
    def kotlinSrc = []
    def execution = []

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*Impl.*', '**/databinding/**/*.*', '**/di/**/*.*', '**/microya/Plugin.kt']

    rootProject.subprojects.each { proj ->
        javaClasses   << fileTree(dir: "$proj.buildDir/intermediates/javac/debug", excludes: fileFilter)
        kotlinClasses << fileTree(dir: "$proj.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
        javaSrc       << "$proj.projectDir/src/main/java"
        kotlinSrc     << "$proj.projectDir/src/main/kotlin"
        execution     << fileTree(
                dir: proj.buildDir,
                includes: [
                        'jacoco/testDebugUnitTest.exec',
                        'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
                ]
        )
    }

    sourceDirectories.from = [javaSrc, kotlinSrc]
    classDirectories.from = [javaClasses, kotlinClasses]

    print execution

    executionData.from = execution

    doLast() {
        println "file://${reports.xml.destination}"
        println "file://${reports.html.destination}/index.html"
    }
}
